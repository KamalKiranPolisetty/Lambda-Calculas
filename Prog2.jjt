/**
 *
 * Copyright (c) 1996-1997 Sun Microsystems, Inc.
 *
 * Use of this file and the system it is part of is constrained by the
 * file COPYRIGHT in the root directory of this system.
 *
 */

options {
  MULTI=true; // an option for JJTree
              // set to build a multi mode tree
              // (with different types of nodes)
  LOOKAHEAD=2;
  FORCE_LA_CHECK=true;
}

PARSER_BEGIN(Prog2)
import java.util.Scanner;
import java.util.HashSet;
import java.util.Set;

class Prog2 {
  public static void main(String args[]) {
    System.out.println(">>> Lambda Expression Evaluator <<<");
    System.out.println("Enter an applied Lambda expression:");
    Prog2 t = new Prog2(System.in);
    try {
      ASTStart n = t.Start();
      n.dump("");
      System.out.println();
      System.out.println("The Lambda expression in the concrete syntax:");
      System.out.println(n.astToString());
      System.out.println();
      System.out.println("The abstract syntax tree with free variables:");
      n.dumpFV("");
      Set<String> freeVariables = n.FV();
      System.out.println();
      System.out.println("Enter an applied Lambda expression:");
      t.ReInit(System.in);
      n = t.Start();
      System.out.println();
      System.out.println("The abstract syntax tree:");
      n.dump("");
      System.out.println();
      System.out.println("Enter the variable to be substituted:");
      Scanner scanner = new Scanner(System.in);
      String var = scanner.nextLine();
      System.out.println();
      System.out.println("Enter the substituting applied Lambda expression:");
      t.ReInit(System.in);
      SimpleNode subExpr = t.Start();
      System.out.println();
      System.out.println("The abstract syntax tree:");
      subExpr.dump("");
      System.out.println();
      SimpleNode substituted = n.substitute(var, subExpr);
      System.out.println("The substitution result:");
      substituted.dump("");
      System.out.println();
      System.out.println("Enter an applied Lambda expression:");
      t.ReInit(System.in);
      SimpleNode NOE = t.Start();
      System.out.println();
      SimpleNode startNode = (SimpleNode) NOE.jjtGetChild(0);
      SimpleNode result = startNode.cloneTree();
      result = result.normalOrderEvaluate();
      System.out.println("The normal order evaluation result:");
      result.dump("");
      System.out.println();
      
    } catch (Exception e) {
      System.out.println("Error has occured please check below trace error");
      System.out.println(e.getMessage());
      e.printStackTrace();
    }
  }
}

PARSER_END(Prog2)


SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
| <"//" (~["\n","\r"])* ("\n"|"\r"|"\r\n")>
| <"/*" (~["*"])* "*" (~["/"] (~["*"])* "*")* "/">
}

TOKEN: /*LAMBDA*/
{
	<LAMBDA: "L">
}

TOKEN : /* LITERALS */
{
  < INTEGER_LITERAL:
        <DECIMAL_LITERAL> (["l","L"])?
      | <HEX_LITERAL> (["l","L"])?
      | <OCTAL_LITERAL> (["l","L"])?
  >
|
  < #DECIMAL_LITERAL: ["1"-"9"] (["0"-"9"])* >
|
  < #HEX_LITERAL: "0" ["x","X"] (["0"-"9","a"-"f","A"-"F"])+ >
|
  < #OCTAL_LITERAL: "0" (["0"-"7"])* >
}

TOKEN : /* IDENTIFIERS */
{
  < IDENTIFIER: <LETTER> (<LETTER>|<DIGIT>)* >
|
  < #LETTER: ["_","a"-"z","A"-"Z"] >
|
  < #DIGIT: ["0"-"9"] >
}



ASTStart Start() : {}
{
  Expression()
  { return jjtThis; }
}



void Expression() #void : {}
{
  (
    MyID() | "+" #add | "-" #sub | "*" #mul | "/" #div
    |Integer() 
    |"(" ("L" MyID() ExpressionBody() #lamb(2))")"
    |"(" Expression() (Expression() #appl(2))* ")"
  )  
   
}

void ExpressionBody() #void : {} 
{
  (
    "." Expression() (Expression() #appl(2))*
    |MyID() ExpressionBody() #lamb(2)
 )
}

void MyID() :
{
  Token t;
}
{
  t=<IDENTIFIER>
  {
    jjtThis.setName(t.image);
  }
}

void Integer() : 
{
	Token t;
}
{
  t=<INTEGER_LITERAL>
{
	jjtThis.setValue(t.image);
}
}
